PARSER_BEGIN(Assignment)

import java.util.Map;
import java.util.HashMap;
import java.util.List;
import java.util.ArrayList;
import java.util.Stack;


public class Assignment {
    public static void main(String args []) {
        Assignment parser = new Assignment(System.in);

        try {
            int result = parser.program();
            System.out.println("PASS");
            System.out.println(result);

        } catch (ParseException e) {
            System.out.println("FAIL");

            int line = e.currentToken.next.beginLine;
            String message = e.getMessage();

            System.err.println(line);
            System.err.println(message);

        } catch (DivergenceException e) {
            System.out.println("PASS");
            System.out.println("DIVERGENCE");

        }
    }
}


interface IEvaluate {
    public int evaluate(String param, int arg) throws ParseException, DivergenceException;
}


class DivergenceException extends Exception {
    public DivergenceException() {
      super();
    }

    public DivergenceException(String message) {
      super(message);
    }
}

class Literal implements IEvaluate {
    private int constant;
    private String var;
    private String f_call;
    private IEvaluate f_arg;
    private Type type;

    public Literal(int constant) {
        this.constant = constant;
        this.type = Type.CONST;
    }

    public Literal(String var) {
        this.var = var;
        this.type = Type.VAR;
    }

    public Literal(String f_call, IEvaluate f_arg) {
        this.f_call = f_call;
        this.f_arg = f_arg;
        this.type = Type.CALL;
    }

    @Override
    public int evaluate(String param, int arg) throws ParseException, DivergenceException {
        switch(this.type) {
            case CONST:
                // Return the boxed constant
                return constant;
            case VAR:
                // Check if the variable matches the parent functions parameter
                if (this.var.equals(param)) {
                    return arg;
                } else {
                    throw new ParseException("undefined variable " + this.var);
                }
            case CALL:
                // Evaluate the function argument
                int value = this.f_arg.evaluate(param, arg);
                // Call the function with the evaluation
                return Function.call(this.f_call, value);
        }

        throw new ParseException("unknown literal type");
    }

    private enum Type {
        CONST, VAR, CALL
    }
}

class BinFold implements IEvaluate {
    private final IEvaluate base;
    private final List<IEvaluate> operands;
    private final Type type;

    public BinFold(IEvaluate base, List<IEvaluate> operands, Type type) {
        this.base = base;
        this.operands = operands;
        this.type = type;
    }

    @Override
    public int evaluate(String param, int arg) throws ParseException, DivergenceException {
        // Evaluate the base into result
        int result = base.evaluate(param, arg);

        // Fold the other terms into result using the appropriate binary op
        for (IEvaluate o : operands){
            switch (type) {
                case MUL:
                    result *= o.evaluate(param, arg);
                    break;
                case PLUS:
                    result += o.evaluate(param, arg);
                    break;
                default:
                    throw new ParseException("unknown bin op type");
            }
        }
        return result;
    }

    enum Type {
        MUL, PLUS
    }
}

class Function {
    private static final Map<String, Function> functions = new HashMap();
    private static final Stack<String> call_stack = new Stack();

    public static int call(String name, int arg) throws ParseException, DivergenceException {
        // Check the function is defined
        if (Function.functions.containsKey(name)) {
            // Call the function
            Function f = functions.get(name);
            return f.call(arg);
        } else {
            throw new ParseException("undefined function " + name);
        }
    }

    public static void define(String name, String param, IEvaluate body) throws ParseException {
        // Check the definition is distinct
        if (Function.functions.containsKey(name)) {
            throw new ParseException("duplicate function definition " + name);
        } else {
            // Register a function object in the programs function map
            Function.functions.put(name, new Function(name, param, body));
        }
    }

    public final String name;
    public final String param;
    public final IEvaluate body;

    private Function(String name, String param, IEvaluate body) {
        this.name = name;
        this.param = param;
        this.body = body;
    }

    public int call(int arg) throws ParseException, DivergenceException {
        // Check for a recursive stack trace
        if (Function.call_stack.contains(name)) {
            throw new DivergenceException();
        }

        // Evaluate the function call and regulate the call stack
        Function.call_stack.push(name);
        int result = this.body.evaluate(this.param, arg);
        Function.call_stack.pop();

        return result;
    }
}

PARSER_END(Assignment)

// No skipped tokens, unnecessary tokens should result in a failed parse

// PLM token descriptor
TOKEN : {
    <DEF: "DEF"> | <MAIN: "MAIN"> | "(" | ")" | "{" | "}" | ";" |
    <SPACE: " "> | <NEWLINE: "\n"> |
    <PLUS: "+"> | <MUL: "*"> |
    <INTEGER_LITERAL: (["0"-"9"])+> |
    <FUNCTION_NAME: (["A"-"Z"])+> |
    <PARAMETER_NAME: (["a"-"z"])+>
}

IEvaluate factor():
{ Token t; IEvaluate arg; }
{
    t = <INTEGER_LITERAL>
        // Return the constant value boxed as a Literal
        { return new Literal(Integer.parseInt(t.image)); }

    | t = <PARAMETER_NAME>
        // Return the parameter reference boxed as a Literal
        { return new Literal(t.image); }

    | t = <FUNCTION_NAME> "(" arg = expr() ")"
        // Return the function call boxed as a Literal
        { return new Literal(t.image, arg); }
}

IEvaluate term():
{ IEvaluate lhs, rhs; List<IEvaluate> operands = new ArrayList(); }
{
    lhs = factor() (LOOKAHEAD(2) <MUL> rhs = term()
        { operands.add(rhs); }
    )*
    // Return an object respresentation of a multiplicative fold on factors
    { return new BinFold(lhs, operands, BinFold.Type.MUL); }
}

IEvaluate expr():
{ IEvaluate lhs, rhs; List<IEvaluate> operands = new ArrayList(); }
{
    lhs = term() (LOOKAHEAD(2) <PLUS> rhs = expr()
        { operands.add(rhs); }
    )*
    // Return an object respresentation of a addative fold on terms
    { return new BinFold(lhs, operands, BinFold.Type.PLUS); }
}

void function():
{ Token t1, t2; IEvaluate body; }
{
    <DEF> " " t1 = <FUNCTION_NAME> " " t2 = <PARAMETER_NAME> " { " body = expr() " } ;" <NEWLINE>
    // Define a new function using the name, parameter and body tokens
    { Function.define(t1.image, t2.image, body); }
}

IEvaluate main():
{ IEvaluate body; }
{
    <DEF> " " <MAIN> " { " body = expr() " } ;" <NEWLINE>
    // Return the main body entry point
    { return body; }
}

int program() throws DivergenceException:
{ IEvaluate body; }
{
    (LOOKAHEAD(3) function())*
    body = main()
    (function())*
    <EOF>
    // After parsing the whole program, evaluate the main body with dummy argument
    { return body.evaluate(null, Integer.MIN_VALUE); }
}
